plugins {
    id 'java-library'
    id 'maven-publish'
}


allprojects {

    group 'dev.implario.games5e'
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.implario.dev/public/'
        }
        maven {
            url 'https://repo.implario.dev/cristalix/'
            credentials {
                username = System.getenv("IMPLARIO_REPO_USER")
                password = System.getenv("IMPLARIO_REPO_PASSWORD")
            }
        }
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'


    dependencies {
//        implementation "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly 'org.projectlombok:lombok:1.18.16'
        annotationProcessor 'org.projectlombok:lombok:1.18.16'

        api 'dev.implario:nettier:1.2.0'
        api 'implario:humanize:1.1.3'
        api 'implario:java-tools:1.1.3'
    }
}


subprojects {

    apply plugin: 'maven-publish'
    publishing {
        repositories {
            maven {
                name 'implario'
                url 'https://repo.implario.dev/public'
                credentials {
                    username System.getenv('IMPLARIO_REPO_USER')
                    password System.getenv('IMPLARIO_REPO_PASSWORD')
                }
            }
        }
    }
    afterEvaluate {
        if (version != 'unspecified') {
            publishing {
                def p = project
                publications {
                    create(p.name, MavenPublication) {
                        it.groupId = p.group
                        it.artifactId = p.description ?: p.name
                        it.version = p.version
                        it.from p.components['java']
                    }
                }
            }
        }
    }


}
